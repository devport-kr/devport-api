plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.graalvm.buildtools.native' version '0.11.3'
}

group = 'kr.devport.backend'
version = '0.0.1-SNAPSHOT'
description = 'Backend API for DevPort'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// OpenAPI/Swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0'

	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-redis-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-security-oauth2-resource-server-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

graalvmNative { //nativeCompile에서만 적용, 
	binaries {
		main {
			imageName = 'devport-api'
			mainClass = 'kr.devport.api.DevportApiApplication'

			buildArgs.add('--verbose')
			buildArgs.add('-H:+ReportExceptionStackTraces')

			buildArgs.add('--gc=serial')
			buildArgs.add('-J-Xmx4g')

			buildArgs.add('--exclude-config')
			buildArgs.add('org\\.springdoc.*')
			buildArgs.add('--initialize-at-build-time=org.slf4j')
		}
	}
}

tasks.named('bootBuildImage') {
	imageName = "devport-api:${project.version}"

	// Build JVM image if property is set, otherwise build native (default with GraalVM plugin)
	if (project.hasProperty('buildJvm')) {
		environment = ['BP_NATIVE_IMAGE': 'false']
	}

	caches {
		cache {
			bind {
				source = file("${System.getProperty('user.home')}/buildpack-cache")
			}
		}
	}
}
